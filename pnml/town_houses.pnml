/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/
/*x---------------------------------------------------------------------------------------------------x*/
/*x------------------------------------------- TOWN HOUSES -------------------------------------------x*/
/*x---------------------------------------------------------------------------------------------------x*/
/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/



/*-----------------------------------------------------------------------------------------------------*/
/*-------------------------------------------- SPRITESETS ---------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritesets for era00 */
/* --------------------------------------------------------------------------------------------------- */

spriteset(spriteset_town_houses_era00_001_g, "gfx/town_houses_era00_001.PNG") {
	tmpl_small_houses_ground (0, 0)
}
spriteset(spriteset_town_houses_era00_001_b, "gfx/town_houses_era00_001.PNG") {
	tmpl_small_houses_building (0, 0)
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------------------ SPRITELAYOUTS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritelayouts for era00 */
/* --------------------------------------------------------------------------------------------------- */

spritelayout spritelayout_town_houses_era00_001 {
	ground { 
		sprite: spriteset_ground_sprite_concrete; }
	childsprite {
		sprite: spriteset_town_houses_era00_001_g; 
		always_draw: 1; }
	building {
		sprite: spriteset_town_houses_era00_001_b;  }
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------- FUNCTIONS/RANDOM SWITCHES/SWITCHES ----------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Switch defining what town types this house type can be built in */
/* --------------------------------------------------------------------------------------------------- */

switch (FEAT_HOUSES, SELF, town_houses_town_type, func_town_type()) {
		1: 0; 2: 1; 3: 0;
}


/* Random switches for the sets of spritelayouts for each era */
/* --------------------------------------------------------------------------------------------------- */

random_switch (FEAT_HOUSES, SELF, random_town_houses_era00) {
		1: spritelayout_town_houses_era00_001;
}


/* Switch for binding the sets of spritelayouts to the historical eras */
/* --------------------------------------------------------------------------------------------------- */

// This switch is called by the 'default' callback and binds the sets of spritelayouts to an animation frame
switch (FEAT_HOUSES, SELF, era_switch_town_houses, animation_frame) {
		0: random_town_houses_era00;
		1: random_town_houses_era00;
}

// This switch is called by the 'anim_control' callback and used for deciding which frame to use
switch (FEAT_HOUSES, SELF, era_anim_town_houses, animation_frame) {
	0:  func_anim_frame;
	CB_RESULT_DO_NOTHING;
}



/*-----------------------------------------------------------------------------------------------------*/
/*--------------------------------------- ITEM DEFINITIONS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

item (FEAT_HOUSES, item_town_houses, 2, HOUSE_SIZE_1X1) {
	property {
		substitute:					06;
		name:						string(STR_TOWNHOUSES);
		population:					25;
		mail_multiplier:			10;
		accepted_cargos:			[[PASS, 4], [MAIL, 1], [GOOD, 0]];
		local_authority_impact:		80;
		probability:				1;
		years_available:			[1700, 1919];
		minimum_lifetime:			10;
		availability_mask:			[ALL_TOWNZONES, ALL_CLIMATES];
		removal_cost_multiplier:	80;
		building_class:				1;
		animation_info:				[ANIMATION_NON_LOOPING, 1];
	}
	graphics {
		default:					era_switch_town_houses;
		anim_control:				era_anim_town_houses;
		construction_check:			town_houses_town_type;
		protection:					func_old_town;
	}
}



