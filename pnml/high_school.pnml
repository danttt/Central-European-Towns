/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/
/*x---------------------------------------------------------------------------------------------------x*/
/*x------------------------------------------- HIGH SCHOOL -------------------------------------------x*/
/*x---------------------------------------------------------------------------------------------------x*/
/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/



/*-----------------------------------------------------------------------------------------------------*/
/*-------------------------------------------- SPRITESETS ---------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritesets for era03 */
/* --------------------------------------------------------------------------------------------------- */

spriteset(spriteset_high_school_era03_001_N_g, "gfx/high_school_era03_001.png") { 		
	tmpl_small_houses_ground (0, 0)
}
spriteset(spriteset_high_school_era03_001_N_b, "gfx/high_school_era03_001.png") {
	tmpl_small_houses_building (0, 0)
}
spriteset(spriteset_high_school_era03_001_W_g, "gfx/high_school_era03_001.png") {
	tmpl_small_houses_ground (0, 100)
}
spriteset(spriteset_high_school_era03_001_W_b, "gfx/high_school_era03_001.png") {
	tmpl_small_houses_building (0, 100)
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------------------ SPRITELAYOUTS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritelayouts for era03 */
/* --------------------------------------------------------------------------------------------------- */

spritelayout spritelayout_high_school_era03_001_N {
	ground { 
		sprite: GROUNDSPRITE_NORMAL; }
	childsprite {
		sprite: spriteset_high_school_era03_001_N_g; 
		always_draw: 1; }
	building {
		sprite: spriteset_high_school_era03_001_N_b;  }
}
spritelayout spritelayout_high_school_era03_001_W {
	ground { 
		sprite: GROUNDSPRITE_NORMAL; }
	childsprite {
		sprite: spriteset_high_school_era03_001_W_g; 
		always_draw: 1; }
	building {
		sprite: spriteset_high_school_era03_001_W_b;  }
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------- FUNCTIONS/RANDOM SWITCHES/SWITCHES ----------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Switch for distance check */
/* --------------------------------------------------------------------------------------------------- */

// High Schools cannot be built closer to each other than at a radius of 20 tiles (manhattan distance)
switch (FEAT_HOUSES, SELF, high_school_distance, nearest_house_matching_criterion(20, SEARCH_HOUSE_BY_TYPE) == 0) {
		1: return 1;
		return 0;
}


/* Switch defining what town types this house type can be built in */
/* --------------------------------------------------------------------------------------------------- */

switch (FEAT_HOUSES, SELF, high_school_town_type, func_town_type()) {
		1: 0; 2: high_school_distance; 3: high_school_distance;
}


/* Random switches for the sets of spritelayouts for each era */
/* --------------------------------------------------------------------------------------------------- */

random_switch (FEAT_HOUSES, SELF, random_high_school_era03_N) {
		1: spritelayout_high_school_era03_001_N;
}
random_switch (FEAT_HOUSES, SELF, random_high_school_era03_W) {
		1: spritelayout_high_school_era03_001_W;
}


/* Switch for binding the sets of spritelayouts to the historical eras */
/* --------------------------------------------------------------------------------------------------- */

// This switch is called by the 'default' callback and binds the sets of spritelayouts to an animation frame
switch (FEAT_HOUSES, SELF, era_switch_high_school_N, animation_frame) {
		random_high_school_era03_N;
}
switch (FEAT_HOUSES, SELF, era_switch_high_school_W, animation_frame) {
		random_high_school_era03_W;
}

// This switch is called by the 'anim_control' callback and used for deciding which frame to use
switch (FEAT_HOUSES, SELF, era_anim_high_school, animation_frame) {
	0:  func_anim_frame;
	CB_RESULT_DO_NOTHING;
}



/*-----------------------------------------------------------------------------------------------------*/
/*--------------------------------------- ITEM DEFINITIONS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

item (FEAT_HOUSES, item_high_school, 40, HOUSE_SIZE_2X1) {
	property {
		substitute:					74;
		name:						string(STR_HIGHSCHOOL);
		population:					30;
		mail_multiplier:			50;
		accepted_cargos:			[[PASS, 8], [MAIL, 4], [GOOD, 2]];
		local_authority_impact:		180;
		probability:				1;
		years_available:			[1920, 9999];
		minimum_lifetime:			20;
		availability_mask:			[ALL_TOWNZONES & ~bitmask(TOWNZONE_EDGE), ALL_CLIMATES];
		removal_cost_multiplier:	160;
		building_class:				1;
        building_flags:             bitmask(HOUSE_FLAG_PROTECTED);
		animation_info:				[ANIMATION_NON_LOOPING, 1];
	}
	graphics {
		graphics_north:				era_switch_high_school_N;
		graphics_west:				era_switch_high_school_W;
		anim_control:				era_anim_high_school;
		construction_check:			high_school_town_type;
	}
}



