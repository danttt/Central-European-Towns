/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/
/*x---------------------------------------------------------------------------------------------------x*/
/*x------------------------------------------- FUNCTIONS ---------------------------------------------x*/
/*x---------------------------------------------------------------------------------------------------x*/
/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/



/*-----------------------------------------------------------------------------------------------------*/
/*---------------------------------------- TOWN TYPE FUNCTION -----------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

// Check for the town type
switch (FEAT_HOUSES, PARENT, func_town_type, population) {
		0 .. 999: return 1;
		1000 .. 9999: return 2;
		return 3;
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------------ HISTORICAL ERA FUNCTIONS ---------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

// Return a number for each historical era
switch (FEAT_HOUSES, SELF, availability_years, current_year) {
		0    .. 1849: return 0;
        1850 .. 1919: return 1;
		1920 .. 1949: return 2;
		1950 .. 1979: return 3;
		1980 .. 2009: return 4;
		return 5;
}

// Determine which animation frame is to be used for each availability era
switch (FEAT_HOUSES, SELF, func_anim_frame, availability_years()) {
		0: 0; 1: 1; 2: 2; 3: 3; 4: 4; 5: 5; }
		


/*-----------------------------------------------------------------------------------------------------*/
/*----------------------------- FUNCTIONS FOR ROAD-AWARE HOUSE TYPES ----------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

// Detect the existence of road class tiles in the immediate vicinity of the house
// Define eight cases for the possible sprites of road-aware house types and return them as numbers
switch (FEAT_HOUSES, SELF, func_road_detect,
		((nearby_tile_class(-1, -1) == TILE_CLASS_ROAD) || (nearby_tile_class(-1, -1) == TILE_CLASS_STATION))*1 +	// N
		((nearby_tile_class(-1,  0) == TILE_CLASS_ROAD) || (nearby_tile_class(-1,  0) == TILE_CLASS_STATION))*2 +	// NE
		((nearby_tile_class(-1,  1) == TILE_CLASS_ROAD) || (nearby_tile_class(-1,  1) == TILE_CLASS_STATION))*4 +	// E
		((nearby_tile_class( 0,  1) == TILE_CLASS_ROAD) || (nearby_tile_class( 0,  1) == TILE_CLASS_STATION))*8 +	// SE
		((nearby_tile_class( 1,  1) == TILE_CLASS_ROAD) || (nearby_tile_class( 1,  1) == TILE_CLASS_STATION))*16 +	// S
		((nearby_tile_class( 1,  0) == TILE_CLASS_ROAD) || (nearby_tile_class( 1,  0) == TILE_CLASS_STATION))*32 +	// SW
		((nearby_tile_class( 1, -1) == TILE_CLASS_ROAD) || (nearby_tile_class( 1, -1) == TILE_CLASS_STATION))*64 +	// W
		((nearby_tile_class( 0, -1) == TILE_CLASS_ROAD) || (nearby_tile_class( 0, -1) == TILE_CLASS_STATION))*128 	// NW
		) {
		131: return 1;	// N corner (no road present in W or E tile)
		199: return 1;  // N corner (road present in W and E tile)
		195: return 1;	// N corner (road present in W tile)
		135: return 1;	// N corner (road present in E tile)
		7:   return 2;	// NE block
		14:  return 3;	// E corner	(no road present in N or S tile)
		31:	 return 3;	// E corner (road present in N and S tile)
		15:  return 3;	// E corner (road present in N tile)
		30:	 return 3;  // E corner (road present in S tile)
		28:  return 4;	// SE block
		56:  return 5;	// S corner	(no road present in E or W tile)
		124: return 5;	// S corner (road present in E and W tile)
		60:  return 5;  // S corner (road present in E tile)
		120: return 5;  // S corner (road present in W tile)
		112: return 6;  // SW block
		224: return 7;	// W corner (no road present in S or N tile)
		241: return 7;  // W corner (road present in S and N tile)
		240: return 7;  // W corner (road present in S tile)
		225: return 7;  // W corner (road present in N tile)
		193: return 8;	// NW block
		return 0;
}

// Combine the output of the historical era and road detection functions
// Output every possible combination as an individual number
switch (FEAT_HOUSES, SELF, func_anim_road, (func_anim_frame()*10) + func_road_detect()) {
		0:   0; 1:   1; 2:   2; 3:   3; 4:   4; 5:   5; 6:   6; 7:   7; 8:   8;
		10: 10; 11: 11; 12: 12; 13: 13; 14: 14; 15: 15; 16: 16; 17: 17; 18: 18;
		20: 20; 21: 21; 22: 22; 23: 23; 24: 24; 25: 25; 26: 26; 27: 27; 28: 28;
		30: 30; 31: 31; 32: 32; 33: 33; 34: 34; 35: 35; 36: 36; 37: 37; 38: 38;
		40: 40; 41: 41; 42: 42; 43: 43; 44: 44; 45: 45; 46: 46; 47: 47; 48: 48;
		50: 50; 51: 51; 52: 52; 53: 53; 54: 54; 55: 55; 56: 56; 57: 57; 58: 58;	
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------- FUNCTIONS FOR OLD TOWN GENERATION -----------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

// Switch chain for generating an old town core typical for Central European towns

// Require a historic unique building ("initial church") to be present within a radius of 5 tiles
switch (FEAT_HOUSES, SELF, func_old_town_2, (
	(nearest_house_matching_criterion(5, SEARCH_HOUSE_BY_TYPE) == 0) && 
	(nearest_house_matching_criterion(5, SEARCH_HOUSE_BY_CLASS) != 0))
	) {
		0: return 0;
		return 1;
}

// Define protection to be limited to the time after 1850 (era00)
switch (FEAT_HOUSES, SELF, func_old_town, (current_year > 1850)) {
		0: return 0;
		func_old_town_2;
}



