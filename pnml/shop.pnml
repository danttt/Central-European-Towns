/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/
/*x---------------------------------------------------------------------------------------------------x*/
/*x---------------------------------------------- SHOP -----------------------------------------------x*/
/*x---------------------------------------------------------------------------------------------------x*/
/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/



/*-----------------------------------------------------------------------------------------------------*/
/*-------------------------------------------- SPRITESETS ---------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritesets for era04 */
/* --------------------------------------------------------------------------------------------------- */

spriteset(spriteset_shop_era04_001_g, "gfx/shop_era04_001.PNG") {
	tmpl_small_houses_ground (0, 0)
}
spriteset(spriteset_shop_era04_001_b, "gfx/shop_era04_001.PNG") {
	tmpl_small_houses_building (0, 0)
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------------------ SPRITELAYOUTS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritelayouts for era04 */
/* --------------------------------------------------------------------------------------------------- */

spritelayout spritelayout_shop_era04_001 {
	ground { 
		sprite: spriteset_ground_sprite_concrete; }
	childsprite {
		sprite: spriteset_shop_era04_001_g; 
		always_draw: 1; }
	building {
		sprite: spriteset_shop_era04_001_b;  }
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------- FUNCTIONS/RANDOM SWITCHES/SWITCHES ----------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Switches defining what town types this house type can be built in */
/* --------------------------------------------------------------------------------------------------- */

switch (FEAT_HOUSES, SELF, shop_v_town_type, func_town_type()) {
		1: 1; 2: 0; 3: 0;
}

switch (FEAT_HOUSES, SELF, shop_t_c_town_type, func_town_type()) {
		1: 0; 2: 1; 3: 1;
}


/* Random switches for the sets of spritelayouts for each era */
/* --------------------------------------------------------------------------------------------------- */

random_switch (FEAT_HOUSES, SELF, random_shop_era04) {
		1: spritelayout_shop_era04_001;
}


/* Switch for binding the sets of spritelayouts to the historical eras */
/* --------------------------------------------------------------------------------------------------- */

// This switch is called by the 'default' callback and binds the sets of spritelayouts to an animation frame
switch (FEAT_HOUSES, SELF, era_switch_shop, animation_frame) {
		random_shop_era04;
}

// This switch is called by the 'anim_control' callback and used for deciding which frame to use
switch (FEAT_HOUSES, SELF, era_anim_shop, animation_frame) {
	0:  func_anim_frame;
	CB_RESULT_DO_NOTHING;
}



/*-----------------------------------------------------------------------------------------------------*/
/*--------------------------------------- ITEM DEFINITIONS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

item (FEAT_HOUSES, item_shop_v, 22, HOUSE_SIZE_1X1) {
	property {
		substitute:					18;
		name:						string(STR_SHOP);
		population:					20;
		mail_multiplier:			12;
		accepted_cargos:			[[PASS, 4], [MAIL, 2], [GOOD, 1]];
		local_authority_impact:		90;
		probability:				7;
		years_available:			[1950, 9999];
		minimum_lifetime:			10;
		availability_mask:			[bitmask(TOWNZONE_CENTRE), ALL_CLIMATES];
		removal_cost_multiplier:	90;
		building_class:				1;
		animation_info:				[ANIMATION_NON_LOOPING, 1];
	}
	graphics {
		default:					era_switch_shop;
		anim_control:				era_anim_shop;
		construction_check:			shop_v_town_type;
	}
}

item (FEAT_HOUSES, item_shop_t_c, 47, HOUSE_SIZE_1X1) {
	property {
		substitute:					18;
		name:						string(STR_SHOP);
		population:					20;
		mail_multiplier:			12;
		accepted_cargos:			[[PASS, 4], [MAIL, 2], [GOOD, 1]];
		local_authority_impact:		90;
		probability:				7;
		years_available:			[1950, 9999];
		minimum_lifetime:			10;
		availability_mask:			[ALL_TOWNZONES & ~bitmask(TOWNZONE_CENTRE), ALL_CLIMATES];
		removal_cost_multiplier:	90;
		building_class:				1;
		animation_info:				[ANIMATION_NON_LOOPING, 1];
	}
	graphics {
		default:					era_switch_shop;
		anim_control:				era_anim_shop;
		construction_check:			shop_t_c_town_type;
	}
}



