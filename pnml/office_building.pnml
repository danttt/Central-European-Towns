/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/
/*x---------------------------------------------------------------------------------------------------x*/
/*x----------------------------------------- OFFICE BUILDING -----------------------------------------x*/
/*x---------------------------------------------------------------------------------------------------x*/
/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/



/*-----------------------------------------------------------------------------------------------------*/
/*-------------------------------------------- SPRITESETS ---------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritesets for era04 */
/* --------------------------------------------------------------------------------------------------- */

spriteset(spriteset_office_building_era04_001_g, "gfx/office_building_era04_001.PNG") {
	tmpl_small_houses_ground (0, 0)
}
spriteset(spriteset_office_building_era04_001_b, "gfx/office_building_era04_001.PNG") {
	tmpl_small_houses_building (0, 0)
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------------------ SPRITELAYOUTS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritelayouts for era04 */
/* --------------------------------------------------------------------------------------------------- */

spritelayout spritelayout_office_building_era04_001 {
	ground { 
		sprite: spriteset_ground_sprite_concrete; }
	childsprite {
		sprite: spriteset_office_building_era04_001_g; 
		always_draw: 1; }
	building {
		sprite: spriteset_office_building_era04_001_b;  }
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------- FUNCTIONS/RANDOM SWITCHES/SWITCHES ----------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Switch for the proximity check */
/* --------------------------------------------------------------------------------------------------- */

switch (FEAT_HOUSES, SELF, office_building_prox,
		func_prox_large_station() == 1) {
		0: return 0;
		return 1;
}


/* Switch defining what town types this house type can be built in */
/* --------------------------------------------------------------------------------------------------- */

switch (FEAT_HOUSES, SELF, office_building_town_type, func_town_type()) {
		1: 0; 2: 0; 3: office_building_prox;
}


/* Random switches for the sets of spritelayouts for each era */
/* --------------------------------------------------------------------------------------------------- */

random_switch (FEAT_HOUSES, SELF, random_office_building_era04) {
		1: spritelayout_office_building_era04_001;
}


/* Switch for binding the sets of spritelayouts to the historical eras */
/* --------------------------------------------------------------------------------------------------- */

// This switch is called by the 'default' callback and binds the sets of spritelayouts to an animation frame
switch (FEAT_HOUSES, SELF, era_switch_office_building, animation_frame) {
		random_office_building_era04;
}

// This switch is called by the 'anim_control' callback and used for deciding which frame to use
switch (FEAT_HOUSES, SELF, era_anim_office_building, animation_frame) {
	0:  func_anim_frame;
	CB_RESULT_DO_NOTHING;
}



/*-----------------------------------------------------------------------------------------------------*/
/*--------------------------------------- ITEM DEFINITIONS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

item (FEAT_HOUSES, item_office_building, 86, HOUSE_SIZE_1X1) {
	property {
		substitute:					29;
		name:						string(STR_OFFICEBUILDING);
		population:					130;
		mail_multiplier:			60;
		accepted_cargos:			[[PASS, 8], [MAIL, 4], [GOOD, 5]];
		local_authority_impact:		130;
		probability:				7;
		years_available:			[1950, 9999];
		minimum_lifetime:			20;
		availability_mask:			[bitmask(TOWNZONE_CENTRE), ALL_CLIMATES];
		removal_cost_multiplier:	140;
		building_class:				1;
		animation_info:				[ANIMATION_NON_LOOPING, 1];
	}
	graphics {
		default:					era_switch_office_building;
		anim_control:				era_anim_office_building;
		construction_check:			office_building_town_type;
	}
}



