/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/
/*x---------------------------------------------------------------------------------------------------x*/
/*x--------------------------------------- TALL OFFICE BUILDING --------------------------------------x*/
/*x---------------------------------------------------------------------------------------------------x*/
/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/



/*-----------------------------------------------------------------------------------------------------*/
/*-------------------------------------------- SPRITESETS ---------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritesets for era05 */
/* --------------------------------------------------------------------------------------------------- */

spriteset(spriteset_tall_office_building_era05_001_g, "gfx/tall_office_building_era05_001.PNG") {
	tmpl_large_houses_ground (0, 0)
}
spriteset(spriteset_tall_office_building_era05_001_b, "gfx/tall_office_building_era05_001.PNG") {
	tmpl_large_houses_building (0, 0)
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------------------ SPRITELAYOUTS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritelayouts for era05 */
/* --------------------------------------------------------------------------------------------------- */

spritelayout spritelayout_tall_office_building_era05_001 {
	ground { 
		sprite: spriteset_ground_sprite_concrete; }
	childsprite {
		sprite: spriteset_tall_office_building_era05_001_g; 
		always_draw: 1; }
	building {
		sprite: spriteset_tall_office_building_era05_001_b;  }
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------- FUNCTIONS/RANDOM SWITCHES/SWITCHES ----------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Switch for the proximity check */
/* --------------------------------------------------------------------------------------------------- */

switch (FEAT_HOUSES, SELF, tall_office_building_prox,
		func_prox_large_station() == 1) {
		0: return 0;
		return 1;
}


/* Switch defining what town types this house type can be built in */
/* --------------------------------------------------------------------------------------------------- */

switch (FEAT_HOUSES, SELF, tall_office_building_town_type, func_town_type()) {
		1: 0; 2: 0; 3: tall_office_building_prox;
}


/* Random switches for the sets of spritelayouts for each era */
/* --------------------------------------------------------------------------------------------------- */

random_switch (FEAT_HOUSES, SELF, random_tall_office_building_era05) {
		1: spritelayout_tall_office_building_era05_001;
}


/* Switch for binding the sets of spritelayouts to the historical eras */
/* --------------------------------------------------------------------------------------------------- */

// This switch is called by the 'default' callback and binds the sets of spritelayouts to an animation frame
switch (FEAT_HOUSES, SELF, era_switch_tall_office_building, animation_frame) {
		random_tall_office_building_era05;
}

// This switch is called by the 'anim_control' callback and used for deciding which frame to use
switch (FEAT_HOUSES, SELF, era_anim_tall_office_building, animation_frame) {
	0:  func_anim_frame;
	CB_RESULT_DO_NOTHING;
}



/*-----------------------------------------------------------------------------------------------------*/
/*--------------------------------------- ITEM DEFINITIONS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

item (FEAT_HOUSES, item_tall_office_building, 89, HOUSE_SIZE_1X1) {
	property {
		substitute:					28;
		name:						string(STR_TALLOFFICE);
		population:					140;
		mail_multiplier:			60;
		accepted_cargos:			[[PASS, 8], [MAIL, 5], [GOOD, 5]];
		local_authority_impact:		140;
		probability:				3;
		years_available:			[1980, 9999];
		minimum_lifetime:			20;
		availability_mask:			[bitmask(TOWNZONE_CENTRE), ALL_CLIMATES];
		removal_cost_multiplier:	170;
		building_class:				1;
		animation_info:				[ANIMATION_NON_LOOPING, 1];
	}
	graphics {
		default:					era_switch_tall_office_building;
		anim_control:				era_anim_tall_office_building;
		construction_check:			tall_office_building_town_type;
	}
}



