/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/
/*x---------------------------------------------------------------------------------------------------x*/
/*x--------------------------------------------- THEATRE ---------------------------------------------x*/
/*x---------------------------------------------------------------------------------------------------x*/
/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/



/*-----------------------------------------------------------------------------------------------------*/
/*-------------------------------------------- SPRITESETS ---------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritesets for era03 */
/* --------------------------------------------------------------------------------------------------- */

spriteset(spriteset_theatre_era03_001_g, "gfx/theatre_era03_001.PNG") { 	
	tmpl_small_houses_ground (0, 0)
}
spriteset(spriteset_theatre_era03_001_b, "gfx/theatre_era03_001.PNG") {
	tmpl_small_houses_building (0, 0)
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------------------ SPRITELAYOUTS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritelayouts for era03 */
/* --------------------------------------------------------------------------------------------------- */

spritelayout spritelayout_theatre_era03_001 {
	ground { 
		sprite: spriteset_ground_sprite_concrete; }
	childsprite {
		sprite: spriteset_theatre_era03_001_g; 
		always_draw: 1; }
	building {
		sprite: spriteset_theatre_era03_001_b;  }
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------- FUNCTIONS/RANDOM SWITCHES/SWITCHES ----------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Switch for distance check */
/* --------------------------------------------------------------------------------------------------- */

// Theatres cannot be built closer to each other than at a radius of 10 tiles (manhattan distance)
switch (FEAT_HOUSES, SELF, theatre_distance, nearest_house_matching_criterion(10, SEARCH_HOUSE_BY_TYPE) == 0) {
		1: return 1;
		return 0;
}


/* Switch defining what town types this house type can be built in */
/* --------------------------------------------------------------------------------------------------- */

switch (FEAT_HOUSES, SELF, theatre_town_type, func_town_type()) {
		1: 0; 2: theatre_distance; 3: 0;
}


/* Random switches for the sets of spritelayouts for each era */
/* --------------------------------------------------------------------------------------------------- */

random_switch (FEAT_HOUSES, SELF, random_theatre_era03) {
		1: spritelayout_theatre_era03_001;
}


/* Switch for binding the sets of spritelayouts to the historical eras */
/* --------------------------------------------------------------------------------------------------- */

// This switch is called by the 'default' callback and binds the sets of spritelayouts to an animation frame
switch (FEAT_HOUSES, SELF, era_switch_theatre, animation_frame) {
		random_theatre_era03;
}

// This switch is called by the 'anim_control' callback and used for deciding which frame to use
switch (FEAT_HOUSES, SELF, era_anim_theatre, animation_frame) {
	0:  func_anim_frame;
	CB_RESULT_DO_NOTHING;
}



/*-----------------------------------------------------------------------------------------------------*/
/*--------------------------------------- ITEM DEFINITIONS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

item (FEAT_HOUSES, item_theatre, 42, HOUSE_SIZE_1X1) {
	property {
		substitute:					31;
		name:						string(STR_THEATRE);
		population:					25;
		mail_multiplier:			30;
		accepted_cargos:			[[PASS, 6], [MAIL, 2], [GOOD, 4]];
		local_authority_impact:		160;
		probability:				1;
		years_available:			[1920, 9999];
		minimum_lifetime:			20;
		availability_mask:			[bitmask(TOWNZONE_INNER_SUBURB, TOWNZONE_CENTRE), ALL_CLIMATES];
		removal_cost_multiplier:	130;
		building_class:				1;
		animation_info:				[ANIMATION_NON_LOOPING, 1];
	}
	graphics {
		default:					era_switch_theatre;
		anim_control:				era_anim_theatre;
		construction_check:			theatre_town_type;			
	}
}



