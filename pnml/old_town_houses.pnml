/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/
/*x---------------------------------------------------------------------------------------------------x*/
/*x----------------------------------------- OLD TOWN HOUSES -----------------------------------------x*/
/*x---------------------------------------------------------------------------------------------------x*/
/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/



/*-----------------------------------------------------------------------------------------------------*/
/*-------------------------------------------- SPRITESETS ---------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritesets for era01 */
/* --------------------------------------------------------------------------------------------------- */

spriteset(spriteset_old_town_houses_era01_001_g, "gfx/old_town_houses_era01_001.PNG") {
	tmpl_small_houses_ground (0, 0)
}
spriteset(spriteset_old_town_houses_era01_001_b, "gfx/old_town_houses_era01_001.PNG") {
	tmpl_small_houses_building (0, 0)
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------------------ SPRITELAYOUTS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritelayouts for era01 */
/* --------------------------------------------------------------------------------------------------- */

spritelayout spritelayout_old_town_houses_era01_001 {
	ground { 
		sprite: spriteset_ground_sprite_concrete; }
	childsprite {
		sprite: spriteset_old_town_houses_era01_001_g; 
		always_draw: 1; }
	building {
		sprite: spriteset_old_town_houses_era01_001_b;  }
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------- FUNCTIONS/RANDOM SWITCHES/SWITCHES ----------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Switches defining what town types this house type can be built in */
/* --------------------------------------------------------------------------------------------------- */

switch (FEAT_HOUSES, SELF, old_town_houses_town, func_town_type()) {
		1: 0; 2: 1; 3: 0;
}

switch (FEAT_HOUSES, SELF, old_town_houses_city, func_town_type()) {
		1: 0; 2: 1; 3: 1;
}


/* Switch for varying attribution to town types in different eras */
/* --------------------------------------------------------------------------------------------------- */

switch (FEAT_HOUSES, SELF, old_town_houses_t_c_switch, func_anim_frame()) {
		1: old_town_houses_city;
		old_town_houses_town;
}


/* Random switches for the sets of spritelayouts for each era */
/* --------------------------------------------------------------------------------------------------- */

random_switch (FEAT_HOUSES, SELF, random_old_town_houses_era01) {
		1: spritelayout_old_town_houses_era01_001;
}


/* Switch for binding the sets of spritelayouts to the historical eras */
/* --------------------------------------------------------------------------------------------------- */

// This switch is called by the 'default' callback and binds the sets of spritelayouts to an animation frame
switch (FEAT_HOUSES, SELF, era_switch_old_town_houses, animation_frame) {
		random_old_town_houses_era01;
}

// This switch is called by the 'anim_control' callback and used for deciding which frame to use
switch (FEAT_HOUSES, SELF, era_anim_old_town_houses, animation_frame) {
	0:  func_anim_frame;
	CB_RESULT_DO_NOTHING;
}



/*-----------------------------------------------------------------------------------------------------*/
/*--------------------------------------- ITEM DEFINITIONS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

item (FEAT_HOUSES, item_old_town_houses, 2, HOUSE_SIZE_1X1) {
	property {
		substitute:					06;
		name:						string(STR_TOWNHOUSES);
		population:					25;
		mail_multiplier:			10;
		accepted_cargos:			[[PASS, 4], [MAIL, 1], [GOOD, 0]];
		local_authority_impact:		80;
		probability:				1;
		years_available:			[1700, 1919];
		minimum_lifetime:			10;
		availability_mask:			[ALL_TOWNZONES & ~bitmask(TOWNZONE_CENTRE), ALL_CLIMATES];
		removal_cost_multiplier:	80;
		building_class:				1;
		animation_info:				[ANIMATION_NON_LOOPING, 1];
	}
	graphics {
		default:					era_switch_old_town_houses;
		anim_control:				era_anim_old_town_houses;
		construction_check:			old_town_houses_t_c_switch;
		protection:					func_old_town;
	}
}



