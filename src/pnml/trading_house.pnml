/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/
/*x---------------------------------------------------------------------------------------------------x*/
/*x------------------------------------------ TRADING HOUSE ------------------------------------------x*/
/*x---------------------------------------------------------------------------------------------------x*/
/*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx*/



/*-----------------------------------------------------------------------------------------------------*/
/*-------------------------------------------- SPRITESETS ---------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritesets for era01 */
/* --------------------------------------------------------------------------------------------------- */

spriteset(spriteset_trading_house_era01_001_g, "gfx/trading_house_era01_001.PNG") { 	
	tmpl_small_houses_ground (0, 0)
}
spriteset(spriteset_trading_house_era01_001_b, "gfx/trading_house_era01_001.PNG") {
	tmpl_small_houses_building (0, 0)
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------------------ SPRITELAYOUTS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Spritelayouts for era01 */
/* --------------------------------------------------------------------------------------------------- */

spritelayout spritelayout_trading_house_era01_001 {
	ground { 
		sprite: spriteset_ground_sprite_concrete; }
	childsprite {
		sprite: spriteset_trading_house_era01_001_g; 
		always_draw: 1; }
	building {
		sprite: spriteset_trading_house_era01_001_b;  }
}



/*-----------------------------------------------------------------------------------------------------*/
/*------------------------------- FUNCTIONS/RANDOM SWITCHES/SWITCHES ----------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

/* Switch defining what town types this house type can be built in */
/* --------------------------------------------------------------------------------------------------- */

switch (FEAT_HOUSES, SELF, trading_house_town_type, func_town_type()) {
		1: 0; 2: 0; 3: 1;
}


/* Switch for varying availability in different eras */
/* --------------------------------------------------------------------------------------------------- */

// Trading houses are city-exclusive unlike other era01 old town buildings built in towns during era02
switch (FEAT_HOUSES, SELF, trading_house_availability_switch, func_anim_frame()) {
		1: trading_house_town_type;
		return 0;
}


/* Random switches for the sets of spritelayouts for each era */
/* --------------------------------------------------------------------------------------------------- */

random_switch (FEAT_HOUSES, SELF, random_trading_house_era01) {
		1: spritelayout_trading_house_era01_001;
}


/* Switch for binding the sets of spritelayouts to the historical eras */
/* --------------------------------------------------------------------------------------------------- */

// This switch is called by the 'default' callback and binds the sets of spritelayouts to an animation frame
switch (FEAT_HOUSES, SELF, era_switch_trading_house, animation_frame) {
		random_trading_house_era01;
}

// This switch is called by the 'anim_control' callback and used for deciding which frame to use
switch (FEAT_HOUSES, SELF, era_anim_trading_house, animation_frame) {
	0:  func_anim_frame;
	CB_RESULT_DO_NOTHING;
}



/*-----------------------------------------------------------------------------------------------------*/
/*--------------------------------------- ITEM DEFINITIONS --------------------------------------------*/
/*-----------------------------------------------------------------------------------------------------*/

item (FEAT_HOUSES, item_trading_house, 19, HOUSE_SIZE_1X1) {
	property {
		substitute:					16;
		name:						string(STR_TRADINGHOUSE);
		population:					100;
		mail_multiplier:			50;
		accepted_cargos:			[[PASS, 8], [MAIL, 4], [GOOD, 3]];
		local_authority_impact:		140;
		probability:				7;
		years_available:			[1700, 1919];
		minimum_lifetime:			10;
		availability_mask:			[bitmask(TOWNZONE_CENTRE), ALL_CLIMATES];
		removal_cost_multiplier:	120;
		building_class:				4;
        animation_info:				[ANIMATION_NON_LOOPING, 1];
	}
	graphics {
		default:					era_switch_trading_house;
        anim_control:               era_anim_trading_house;
		construction_check:			trading_house_availability_switch;
		protection:					func_old_town;
	}
}



